import edu.wpi.first.nativeutils.vendordeps.WPIVendorDepsPlugin

buildscript {
  repositories {
    maven {
      url = 'https://frcmaven.wpi.edu/artifactory/ex-mvn'
    }
  }
}

plugins {
  id 'maven-publish'
  id 'java-library'
  id 'edu.wpi.first.GradleRIO' version '2024.3.2'
  id 'edu.wpi.first.wpilib.repositories.WPILibRepositoriesPlugin' version '2020.2'
}

if (project.hasProperty('releaseMode')) {
    wpilibRepositories.addAllReleaseRepositories(project)
} else {
    wpilibRepositories.addAllDevelopmentRepositories(project)
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/spacey-sooty/revlib-sim")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17

    withSourcesJar()
    withJavadocJar()
}

dependencies {
    api wpi.java.deps.wpilib() // ntcore, wpiutil (datalog, struct), wpiunits
    implementation wpi.java.vendor.java()
}

group = "au.jade"
version = "2024.1.1"
